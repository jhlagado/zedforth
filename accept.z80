; accept ptr max -- len    
accept:
    _enter
    _unjoin
    
    _over                           ; ptr max ptr
    _plus                           ; ptr ptr+max
    _oneMinus                       ; ptr1 ptr2
    _over                           ; ptr1 ptr2 ptr
    ld BC,HL                        
    _drop                           ; ptr1 ptr2             BC: ptr
accept1:
    _call key                       ; ptr1 ptr2 key
    ld A,L
    _call emit                      ; ptr1 ptr2
    bit 7,A                                                         ; EOF? used by tests
    jr nz,accept3
    cp "\r"
    jr z,accept3
    cp "\b"
    jr z,accept2
    _dup                            ; ptr1 ptr2 ptr2
    or a
    sbc HL,BC                       ; ptr1 ptr2 ptr2-BC     c: BC > ptr2
    _drop                           ; ptr1 ptr2                                                                                                                
    jr c,accept1                    ; ignore key
    ld (BC),A                       ; write key
    inc BC                          ; ptr1 ptr2             BC: ptr+1
    jr accept1
accept2:    
    _over                           ; ptr1 ptr2 ptr1    
    or A
    sbc HL,BC                       ; ptr1 ptr2 ptr1-BC     nc: BC < ptr1
    _drop
    jr nc,accept1                   ; ignore backspace
    dec BC                          ; ptr1 ptr2             BC: ptr-1
    jr accept1
accept3:
    _drop                           ; ptr1                  BC: ptr
    ld DE,BC                        ; ptr ptr1
    ex DE,HL
    or A
    sbc HL,DE                       ; len

    _rejoin
    _exit
    
