; BC = IP
; DE = RSP
; HL = transient (used for jump)
; DE' = transient (used for intermediate results)
; HL' = TOS
; BC',IX,IY = unused

.macro _initKernel
    ld BC,startForth
    ld DE,rStack 
    ld IY,next16
    ld HL, FALSE
    ld (var_state),HL
    ld HL, 10
    ld (var_base),HL
    ld HL, (initialLatest)
    ld (var_latest),HL
    ld HL, START_USER
    ld (var_here),HL
.endm

.macro _next            ; 28t       EX1
	jp (IY)
.endm

.macro _enter           ; 73t       EX1
    _rpush B,C			
    pop BC			
    _next			
.endm

.macro _exit            ; 62t       EX1
    _rpop B,C
    _next
.endm

.macro _mode8
    ld IY,next8
    _next
.endm

.macro _mode16
    ld IY,next16
    _next
.endm

.macro _quit
    halt
.endm