; word ( delim "<chars>ccc<char>" -- c-addr ) 
; read a word from input stream
; Skip leading delimiters. Parse characters ccc delimited by char. 
word:
    _enter
    _unjoin
    
    _call skipDelims                        ; delim
    _fetch var_here                         ; delim ptr
    _onePlus                                ; delim ptr'        ; fill in length later
    _push $40                               ; delim ptr' maxlen
    _call wordRead                          ; ptr len
    _swap                                   ; len ptr'
    _oneMinus                               ; len ptr
    _tuck                                   ; ptr len ptr   
    _cbang                                  ; ptr               ; store length

    _rejoin
    _exit                                     

; delim -- delim
skipDelims:                                 
    _enter
    _unjoin

skipd1:
    _call key                               ; delim key
    ld A,L                                  ; delim key    
    _drop                                   ; delim
    cp L                                    ; delim             ; compare with delim
    jr z,skipd1                             ; delim             ; if delim then loop
    cp $1F                                  ; delim             ; whitespace
    jr c,skipd1
    cp $5C                                  ; delim             ; \ start of a comment?
    jr z,skipd2
    bit 7,A                                 ; delim             ; EOF? used by tests
    jr nz,skipd3
    _call unkey                             ; delim             ; unread the key                             
    jr skipd3
skipd2:                                                         ; skip to end of line
    _call key                               ; delim key2
    ld A,L                                  ; delim key2    
    _drop                                   ; delim
    cp "\n"
    jr nz,skipd2                                                ; skip comment loop
    jr skipd1                                                   ; main loop
skipd3:

    _rejoin
    _exit                                     

; delim ptr maxlen -- ptr len
wordRead:                                   
    _enter
    _unjoin
    
    _swap                                   ; delim len ptr
    ld BC,HL                                ; delim len ptr         ; BC: ptr    
    _swap                                   ; delim ptr len
    _rot                                    ; ptr len delim 
    _swap                                   ; ptr delim len 
    jr wordRead2
wordRead1:
    _swap                                   ; ptr len delim
    _call key                               ; ptr len delim key
    _dup
    _call emit
    ld A,L
    _drop                                   ; ptr len delim
    cp L                                                            ; key = delim?
    jr z,wordRead3
    cp " "                                                          ; < space? i.e. control chars
    jr c,wordRead3
    bit 7,A                                                         ; EOF? used by tests
    jr nz,wordRead3
    ld (BC),A                            
    inc BC
    _swap                                   ; ptr delim len
wordRead2:
    _loopNext wordRead1                     ; ptr delim
    jr wordRead4
wordRead3:
    _drop                                   ; ptr len
wordRead4:
    _drop                                   ; ptr
    _dup
    ld HL,BC                                ; ptr ptr'
    _over                                   ; ptr ptr' ptr
    _minus                                  ; ptr len

    _rejoin
    _exit
    
 