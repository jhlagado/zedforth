; -- n
; depth of data stack
.macro _depth              
    _spAt
    _push dStack
    _swap
    _minus    
    _twoSlash
.endm

; x y -- z x y   N: z --
.macro _unnip			    ; 12t		
	dec SP	
    dec SP	
.endm

; x y -- z x y   N: z --
.macro _twoUnnip			; 24t		
	_unnip	
    _unnip	
.endm

.macro _dup
    push HL			        ; 11t
.endm

; x1 --  
.macro _drop                ; 10t			
    pop HL			            
.endm

; x1 x2 -- x2 x1
.macro _swap                ; 19t			
    ex (SP),HL		
.endm

; x1 x2 -- x1 x2 x1
.macro _over 	            ; 36t			
    pop DE			            ; 10t		x2			T: x1
    push DE			            ; 11t		x1 x2		T: x1
	_dup			            ; 11t
	ex DE,HL			        ; 4t
.endm

; ( x1 x2 x3 -- x2 x3 x1 )
.macro _rot	                ; 45t			
    pop DE		                ; 10t
    ex DE,HL			        ; 4t		x1 x3 			T: x2
    ex (SP),HL			        ; 20t		x2 x3 			T: x1
	_dup			            ; 11t
.endm

; x1 x2 -- x2  		
.macro _nip 	            ; 14t			
	inc SP	
    inc SP	
.endm

; x1 x2 -- x2 x1 x2
.macro _tuck				        
	_swap				; 25t
    _over				; 35t
.endm

; x1 x2 -- x1 x2 x1 x2
.macro _twoDup      ; 43t         	
    pop DE			    ; 10t		; x2				T: x1
    push DE			    ; 11t		; x1 x2				T: x1
	_dup				; 11t		; x1 x2 x2			T: x1
	push DE			    ; 11t		; x1 x2 x1 x2
.endm

.macro _twoDrop     ; 20t       	; x x --     
    _drop				; 10t
	_drop				; 10t
.endm

; x1 x2 x3 x4 -- x3 x4 x1 x2 	
.macro _twoSwap	    ; 99t 			
    pop DE              ; 10		x1 x2 x4            T x3
    ex (SP),HL			; 19		x1 x4 x2			T x3
    ex DE,HL            ; 4         x1 x4 x3            T x2
    _nip			    ; 12		x1 x3		N x4   	T x2
    ex (SP),HL			; 19		x3 x1				T x2
    _unnip			    ; 12		x3 x4 x1			T x2
	_dup			    ; 11t
    ex DE,HL
.endm

; x1 x2 x3 x4 -- x1 x2 x3 x4 x1 x2	       
.macro _twoOver	    ; 114t			
    _dup				; 11t		x1 x2 x3 x4 x4
	_nip				; 24t		x1 x2 x3 x4	    N: x4
	_nip				; 24t		x1 x2 x4	    N: x3 x4
	_drop				; 10t		x1 x2	        N: x2 x3 x4
	pop DE			    ; 10t		x2		        N: x1 x2 x3 x4      T = x1
    _twoUnnip			; 24t		x1 x2 x2   	    N: x3 x4            T = x1
	_twoUnnip			; 24t		x1 x2 x3 x4 x2                      T = x1
    push DE			    ; 11t		x1 x2 x3 x4 x1 x2	
.endm

; -- n
.macro _spAt
    ex DE,HL
    ld HL,0
    add HL,SP
    ex DE,HL
    _dup
    ex DE,HL
.endm
