
; [compile]   ( -- ; <string> )
; Compile the next immediate word into CODE dictionary.
CODE "[compile]",9,bracCompile
    ENTER 1
    call tick
    call comma
    EXIT 1

; compile     ( -- )
; Compile the next address in colon list to CODE dictionary.
CODE 'compile',7,compile
    _drop                                   ; ret
    _dup                                    ; ret ret
    _at                                     ; ret ret@
    call callc                              ; ret 
    _push 3                                 ; ret 3
    _plus                                   ; ret + 3
    ex DE,HL
    _drop
    jp (HL)

; literal     ( w -- )
; Compile tos to CODE dictionary as an integer literal.
CODE 'literal',IMMED+7,literal
    ENTER 0
    _compile lit
    call comma
    EXIT 0

; CREATE ( -- ; <string> )
; Compile a new array entry without allocating CODE space.
CODE "create",6,create             
    ENTER 0
    _fetch var_here                         ; &here
    _fetch var_latest                           ; here latest
    call comma                              ; write value current value of latest to link field (inc here by 2)
    _store var_latest                           ; store header to latest 
    _push 0                                 ; 0
    call ccomma                             ; write 0 to flags field (inc here by 1)
    _pushc " "                              ; delim
    call word                               ; read word token into name field at here
    _cat                                    ; get name length
    _onePlus                                ; inc by 1 for length byte
    call allot                              ; allot len+1
    _push jp_opcode 
    call ccomma                             ; compile default behavious
    _push create1 
    call comma                              ; i.e. push addr of data area
    EXIT 0
create1:
    call rfrom                              ; push onto stack
    EXIT 0
    
CODE ":",1,colon
    ENTER 0
    call create                             ; create the dictionary entry / header
    _push -3
    call allot                              ; erase jump instruction left by create 
    _fetch var_latest
    call toggleHidden                           ; make the word hidden
    call rbrac                                  ; go into compile mode.
    EXIT 0                                     

; ( -- )
; immediately enter interpretation state. 
CODE "[",IMMED+1,lbrac
    ENTER 0
	_push FALSE
	_bang 
    EXIT 0

; ( -- )
; enter compilation state. 
CODE "]",1,rbrac
    ENTER 0
	_push TRUE
	_bang 
    EXIT 0

; ( -- )
; immediately finish definition and leave compilation state.
; allow defintion to be visible in dictionary
CODE ";",IMMED+1,semicolon
    ENTER 0
    _push RET_opcode 
    call ccomma                             ; compile ret
    _fetch var_latest                           ; get latest defined word
    call toggleHidden                       ; toggle hidden flag -- unhide 
    call lbrac                              ; go back to immediate mode.
    EXIT 0                                

; toggleHidden addr --
; hides word pointed to by addr
toggleHidden:
    ENTER 0
    _cellPlus                               ; pflags
    _dup                                    ; pflags pflags
    _cat                                    ; pflags flags
    _push HIDE                              ; pflags flags HIDE
    call xorr                                    ; pflags flags^HIDE
    _swap                                   ; flags^HIDE pflags
    _cbang
    EXIT 0

; IMMEDIATE   --   
; make last definition immediate
CODE "immediate",9,immediate
    ENTER 0
    _fetch var_latest
    _cellPlus
    _cAt
    _push IMMED
    call orr
    _swap
    _cBang
    EXIT 0

CODE ":noname",7,colonNoName
    ENTER 1
    _fetch var_here                             ; TOS = here
    call rbrac                                  ; go into compile mode.
    EXIT 1                                 

CODE "does>", IMMED+5, does                  ; --
    ENTER 1
    _compile xdoes                          ; compile two calls 
    _compile rfrom                          ; address of body of created word                            
    EXIT 1                                 

xdoes:                                      ; --
    _drop                      ;call rfrom  ; TOS = ptr to body after does> word
    ENTER 1                                  ; caller of caller?

    _fetch var_latest                           ; ptr to created word
    call toBody                            
    _push call_opcode
    _over
    _cBang
    _onePlus
    _bang                                   ; rewrite with address of body after does>
    EXIT 1                                 ; return to caller of caller (terminate caller)

;Z IMMED?    nfa -- f      fetch immediate flag
;   1- C@ ;                     nonzero if ``immed''
CODE "immed?",6,IMMEDQ
    ENTER 1
    _oneMinus
    _at
    _push IMMED
    call andd
    EXIT 1

; NFA>LFA   nfa -- lfa    
; name adr -> link field
;   4 - ;
CODE "LFA>NFA",7,LFAtoNFA
    ENTER 0
    _cellPlus
    _onePlus
    EXIT 0

; NFA>LFA   nfa -- lfa    
; name adr -> link field
;   4 - ;
CODE "NFA>LFA",0,NFAtoLFA
    ENTER 0
    _oneMinus
    _twoMinus
    EXIT 0

; NFA>CFA   nfa -- cfa    name adr -> CODE field
;   COUNT 7F AND + ;       mask off 'smudge' bit
CODE "NFA>body",0,NFAtoBody
    ENTER 0
    _dup
    _cat
    _onePlus                               ; skip over len
    _plus
    EXIT 0

; >body     ( ptr -- ptr' )
CODE ">body",0,toBody
    ENTER 0
    _cellPlus                              ; skip over backlink
    _onePlus                               ; skip over flags
    ; TODO: replace rest with jump to NFATOCFA
    _dup
    _cat
    _onePlus                               ; skip over len
    _plus
    EXIT 0

CODE "'",0,tick
    ENTER 0
    _pushc " "
    call word
    call find
    _drop
    EXIT 0

CODE "[']",IMMED,bracTick
    ENTER 0
    call tick               ; get xt of 'xxx'
    call literal
    EXIT 0

; ( "<spaces>name" -- )
CODE "defer",0,defer
    ENTER 0
    call create
    _compile abort
    call does
    _at
    call execute
    EXIT 0

; ( xt2 xt1 -- )
CODE "defer!",0,deferstore
    ENTER 0
    call toBody
    _bang
    EXIT 0

; ( xt1 -- xt2 )
CODE "defer@",0,deferAt
    ENTER 0
    call toBody
    _at
    EXIT 0

; ( xt "<spaces>name" -- )
CODE "is",IMMED,is
    ENTER 0
    _fetch var_state 
    _isZero
    jr z,is1
    _compile bracTick
    _push deferStore
    call comma
    EXIT 0
is1:
    _push deferStore
    EXIT 0

